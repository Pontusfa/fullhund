buildscript {
    configurations.maybeCreate("pitest")

    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
        pitest 'org.pitest:pitest-junit5-plugin:0.3'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.3.0'
    id 'org.sonarqube' version '2.6'
}

group = 'lk.pontusfa.fullhund'
version = '1.0-SNAPSHOT'
mainClassName = 'lk.pontusfa.fullhund.App'

sourceCompatibility = 9
targetCompatibility = 9

sourceSets {
    cucumber {
        java
        resources
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.7'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    cucumberCompile 'io.cucumber:cucumber-java:2.4.0'
}

test {
    useJUnitPlatform()
}

pitest {
    testPlugin = 'junit5'
    timestampedReports = false
    outputFormats = ['XML', 'HTML']
}

task cucumber {
    dependsOn compileCucumberJava

    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = sourceSets.cucumber.runtimeClasspath
            args = ['--plugin', "json:$buildDir/cucumber/report.json",
                    '--glue', 'lk.pontusfa.fullhund',
                    'src/cucumber/resources']
        }
    }
}

jacoco {
    toolVersion '0.8.1'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}



javadoc {
    options.addBooleanOption('html5', true)
}

distributions {
    main {
        contents {
            from(javadoc) {
                into 'docs'
            }
        }
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.cucumber.java.srcDirs
        testSourceDirs += project.sourceSets.cucumber.resources.srcDirs
    }
}
