buildscript {
    configurations.maybeCreate("pitest")

    dependencies {
        pitest 'org.pitest:pitest-junit5-plugin:0.7'
    }
}

plugins {
    id 'info.solidsoft.pitest' version '1.3.0' apply false
    id "org.sonarqube" version "2.6.2" apply false

    id 'nebula.lint' version '10.1.0'
    id 'com.gradle.build-scan' version '1.16'
}

subprojects {
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    group = 'lk.pontusfa.fullhund'
    sourceCompatibility = 10
    targetCompatibility = 10

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    test {
        useJUnitPlatform()
    }

    pitest {
        testPlugin = 'junit5'
        timestampedReports = false
        outputFormats = ['XML', 'HTML']
        pitestVersion = '1.4.2'
    }

    jacoco {
        toolVersion '0.8.2'
    }

    javadoc {
        options.addBooleanOption('html5', true)
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

gradleLint.rules = ['all-dependency', 'archaic-wrapper', 'duplicate-dependency-class', 'overridden-dependency-version']
