plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.sonarqube' version '2.6'
}

group = 'lk.pontusfa.fullhund'
version = '1.0-SNAPSHOT'
mainClassName = 'lk.pontusfa.fullhund.App'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'io.cucumber:cucumber-java:2.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

test {
    useJUnitPlatform()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--format', 'json:build/cucumber/report.json', '--glue', 'lk.pontusfa.fullhund', 'src/test/resources']
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

jacoco {
    toolVersion '0.8.1'
}

javadoc {
    options.addBooleanOption('html5', true)
}

distributions {
    main {
        contents {
            from(javadoc) {
                into 'docs'
            }
        }
    }
}


