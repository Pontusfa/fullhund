plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'jacoco'
    id 'org.sonarqube' version '2.6'
}

group = 'lk.pontusfa.fullhund'
version = '1.0-SNAPSHOT'
mainClassName = 'lk.pontusfa.fullhund.App'

sourceCompatibility = 9
targetCompatibility = 9

sourceSets {
    cucumber {
        java
        resources
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    cucumberCompile 'io.cucumber:cucumber-java:2.4.0'
}

test {
    useJUnitPlatform()
}

task cucumber {
    dependsOn compileCucumberJava

    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = sourceSets.cucumber.runtimeClasspath
            args = ['--plugin', "json:$buildDir/cucumber/report.json",
                    '--glue', 'lk.pontusfa.fullhund',
                    'src/cucumber/resources']
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

jacoco {
    toolVersion '0.8.1'
}

javadoc {
    options.addBooleanOption('html5', true)
}

distributions {
    main {
        contents {
            from(javadoc) {
                into 'docs'
            }
        }
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.cucumber.java.srcDirs
        testSourceDirs += project.sourceSets.cucumber.resources.srcDirs
    }
}
